:
	on init
		do : dial : !! Dial((:0123456789:))
		do : p : 0
		do : q : 0
		do : mode : DIAL
//		do solo
	else in : mode : DIAL
		on : mode : .
			do : *dial : ((*p,*q),0)
			in solo
				do > "%s x %s = ":< *p, *q >
		else on : %% : ? < *dial
			do : r : %<?:((.,?),.)>
			do : v : %<?:(.,?)>
			do : mode : OUTPUT
	else in : mode : OUTPUT
		in solo
			do > "%s%s\n":< *r:~0, *v >
			do exit
		else on : q : ?
			do > " %s%s ":< (*r:~0?:' '), *v >
			do : mode : DIAL
			in %?: 0
				do >:
				in : p : 9
					do exit
				else do : p : ( \
					(*p:0) ? 1 :	\
					(*p:1) ? 2 :	\
					(*p:2) ? 3 :	\
					(*p:3) ? 4 :	\
					(*p:4) ? 5 :	\
					(*p:5) ? 6 :	\
					(*p:6) ? 7 :	\
					(*p:7) ? 8 :	\
					(*p:8) ? 9 :)
		else do : q : ( \
				(*q:0) ? 1 :	\
				(*q:1) ? 2 :	\
				(*q:2) ? 3 :	\
				(*q:3) ? 4 :	\
				(*q:4) ? 5 :	\
				(*q:5) ? 6 :	\
				(*q:6) ? 7 :	\
				(*q:7) ? 8 :	\
				(*q:8) ? 9 :	\
				(*q:9) ? 0 :)

: Dial
	on : %% : ? < ..
		do : i : %<?:((?,.),.)>
		do : q : %<?:((.,?),.)>
		do : r : %<?:(.,?)>
		do : p : 0
		do : c : 0
	else on : i : ?  // add q to p i times, via n
		in %?: ~0
			do : n : *q
		else in : r : (?:~0)
			do : n : %?
			do : r : 0
		else
			do : .. : ((R,*c), *p )	// notify result
	else on : n : ?
		in %?: 0
			do : i : %(?,(*i,.))	// decrement i
		else
			do : n : %(?,(%?,.))	// decrement n
			in (*p,(?,.))
				do : p : %?	// increment p
			else
				do : p : 0		// reset p to 0
				do : c : %(*c,(?,.))	// increment carry
	else on exit < ..
		do exit

