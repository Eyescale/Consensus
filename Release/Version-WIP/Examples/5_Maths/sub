:
/*
	Uses pre-instantiated literal - aka. dial - e.g.
		(:0123456789:)	==	0 --+---v
					    1 --+---v
						2 --+---v
						    3 --+---v
							... +---v
							    9 --+--\0
	to compute the sum Z of the two numbers X and Y
*/
	on init
		do : dial : !! Dial((:0123456789:))
		do : X : ((X,...):123:)	// no carry
//		do : X : ((X,...):189:)
		do : Y : ((Y,...):13234:)
		do : Z : ((Z,...)::)
		do : mode : INIT
	else in : mode : INIT
		do : p : *X
		do : q : *Y
		do : r : (r,*)
		do : carry : 0
		do : mode : SUB
	else in : mode : SUB
		on : mode : .	// dial operation on next digit(s)
			in : p : (.,?)
				do : *dial : ((%?,(*q?%(*q:(.,?)):0)),*carry)
			else in : q : (.,?)
				do : *dial : ((0,%?),*carry)
			else
				in : carry : 0
					do : r : *r
					do : mode : FORMAT
				else
					do : r : %((r,*),.)
					do : q : ( q, * )
					do : mode : NEG
			do : carry : 0
		else on : %% : ? < *dial
			in %<?:~'\0'>
				do : r : ( *r, %<?> )
				in : p : (?,.)
					do : p : ( %?:(.,*) ? ~. : %? )
				in : q : (?,.)
					do : q : ( %?:(.,*) ? ~. : %? )
				do : mode : SUB
			else do : carry : 1
	else in : mode : NEG
		on : mode : .
			in : r : (.,?)
				do : *dial : ((0,%?),*carry)
				do : carry : 0
			else
				do : r : *q
				do : mode : FORMAT
		else on : %% : ? < *dial
			in %<?:~'\0'>
				do : q : ( *q, %<?> )
				in ?: (*r,.)
					do : r : %?
				else do ~( r )
				do : mode : NEG
			else do : carry : 1
	else in : mode : FORMAT
		on : r : (.,?)	// inform Z from r
			in %?: ~*
				do : Z : ( *Z, %? )
				do : r : %(*r:(?,.))
			else
				do ~( *r )
				do : mode : OUTPUT
	else in : mode : OUTPUT
		on : mode : .
			do > "\t"
			do : p : %((X,*), . )
			do : q : X
		else on : p : (.,?)
			do > "%s" : %?
			in ?: (*p,.)
				do : p : %?
			else in : q : X
				do > " - "
				do : p : %((Y,*), . )
				do : q : Y
			else in : q : Y
				do > " = %s": ((*carry:1)?'-':)
				do : p : %((Z,*), . )
				do : q : Z
			else
				do > "\n"
				do exit

: Dial
/*
	compute p - q - carry (digits) on demand
*/
	on : %% : ? < ..
		do : p : %<?:((?,.),.)>		// base
		do : q : %<?:((.,?),.)>		// offset
		do : carry : %<?:(.,?)>		// carry
	else on : q : ?
		in %?: 0
			in : carry : 0
				do : .. : *p	// notify result
			else
				do : carry : 0
				do : q : 1
		else in (?,(*p,.))
			do : p : %?		// decrement p
			do : q : %(?,(*q,.))	// decrement q
		else
			do : .. : '\0'		// notify carry
	else on : .. : '\0'
		do : p : %(?:~(.,.),'\0')	// reset base
		do : q : %(?,(*q,.))		// decrement q
	else on exit < ..
		do exit

